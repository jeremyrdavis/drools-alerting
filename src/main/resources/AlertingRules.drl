#created on: Oct 6, 2011
package com.jboss.examples.drools.cep.alerts.rules


#list any import classes here.
import java.util.List;
import java.util.ArrayList;
import java.util.Date;

import com.jboss.examples.drools.cep.alerting.model.Device
import com.jboss.examples.drools.cep.alerting.model.DeviceAlert
import com.jboss.examples.drools.cep.alerting.model.CompoundAlert
import com.jboss.examples.drools.cep.alerting.model.RawAlert
import com.jboss.examples.drools.cep.alerting.model.AlertStatus
import com.jboss.examples.drools.cep.alerting.model.RawAlert
import com.jboss.examples.drools.cep.alerting.services.DataService
import com.jboss.examples.drools.cep.alerting.services.AlertingMessageService
import com.jboss.examples.drools.cep.alerting.services.LoggerService

global DataService dataSvc
global AlertingMessageService jmsSvc
global LoggerService logger

declare RawAlert
    @role( event )
end

declare DeviceAlert 
    @role(event) 
end
 
/*
    An DeviceAlert is created from a RawAlert
*/
rule "Create New DeviceAlert When RawAlert Arrives"
    when
        $s : RawAlert( status == AlertStatus.ACTIVE )
    then
        logger.logMessage("initial alert received : " + $s );
        Device $device = dataSvc.lookupDevice( $s.getDeviceId() );
        DeviceAlert a = new DeviceAlert( $s, $device );
        logger.logMessage("InitialAlert received.  Creating DeviceAlert for DeviceAlert " + a.getId());
        retract($s);
        insert(a);
end

rule "Multiple Alerts"
    duration( 1m )
    when
        $a : DeviceAlert()
        $l : List( ) from accumulate( $b : DeviceAlert( deviceName == $a.deviceName, this after[0,1m] $a ), collectList($b) )
    then
        CompoundAlert alertGroup = new CompoundAlert( $a.getDeviceName() );
        alertGroup.addAlerts( $l );
        for ( DeviceAlert alert : new ArrayList<DeviceAlert>((List<DeviceAlert>)$l) ){
            retract( alert );
        }
        logger.logMessage("AlertGroup created: " + alertGroup );
        jmsSvc.sendAlertGroup( alertGroup );
end

rule "Single Alert"
    when
        $a : DeviceAlert()
        not DeviceAlert( this != $a, deviceName == $a.deviceName, this after[0,59s] $a )
    then
        retract( $a );
        logger.logMessage("Single alert processed: " + $a );
end
