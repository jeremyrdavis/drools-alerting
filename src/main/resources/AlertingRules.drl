#created on: Oct 6, 2011
package com.jboss.examples.drools.cep.alerts.rules


#list any import classes here.
import java.util.List;
import java.util.ArrayList;
import java.util.Date;

import com.jboss.examples.drools.cep.alerting.model.Alert
import com.jboss.examples.drools.cep.alerting.model.InitialAlert
import com.jboss.examples.drools.cep.alerting.model.AlertStatus
import com.jboss.examples.drools.cep.alerting.model.DerivedAlert
import com.jboss.examples.drools.cep.alerting.model.InitialAlert
import com.jboss.examples.drools.cep.alerting.services.DataService
import com.jboss.examples.drools.cep.alerting.services.LoggerService

global DataService dataSvc
global JMSService jmsSvc
global LoggerService logger

declare InitialAlert
    @role( event )
end

declare Alert 
    @role(event) 
end
 
/*
    An Alert is created from a InitialAlert
*/
rule "Create New Alert When InitialAlert Arrives"
    when
        $s : InitialAlert( status == AlertStatus.ACTIVE )
    then
        logger.logMessage("InitialAlert received : " + $s );
        Alert a = new Alert( $s );
        a.setUpstreamLink( dataSvc.lookupUpstreamLink( a.getDeviceName() ) );
        a.setDownstreamLink( dataSvc.lookupDownstreamLink( a.getDeviceName() ) );
        logger.logMessage("InitialAlert received.  Creating Alert for Alert " + a.getId());
        retract($s);
        insert(a);
end

rule "Multiple Alerts"
    duration( 1m )
    when
        $a : Alert()
        $l : List( ) from accumulate( $b : Alert( deviceName == $a.deviceName, this after[0,1m] $a ), collectList($b) )
    then
        DerivedAlert derivedAlert = new DerivedAlert( );
        derivedAlert.addAlerts( $l );
        for ( Alert alert : new ArrayList<Alert>((List<Alert>)$l) ){
            retract( alert );
        }
        logger.logMessage("DerivedAlert created: " + derivedAlert );
end

rule "Single Alert"
    when
        $a : Alert()
        not Alert( this != $a, deviceName == $a.deviceName, this after[0,59s] $a )
    then
        retract( $a );
        logger.logMessage("Single alert processed: " + $a );
end
