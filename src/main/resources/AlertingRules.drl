#created on: Oct 6, 2011
package com.jboss.examples.drools.cep.alerts.rules


#list any import classes here.
import java.util.List;
import java.util.ArrayList;
import java.util.Date;

import com.jboss.examples.drools.cep.alerting.model.Alert
import com.jboss.examples.drools.cep.alerting.model.AlertGroup
import com.jboss.examples.drools.cep.alerting.model.RawAlert
import com.jboss.examples.drools.cep.alerting.model.AlertStatus
import com.jboss.examples.drools.cep.alerting.model.DerivedAlert
import com.jboss.examples.drools.cep.alerting.model.RawAlert
import com.jboss.examples.drools.cep.alerting.services.DataService
import com.jboss.examples.drools.cep.alerting.services.AlertingMessageService
import com.jboss.examples.drools.cep.alerting.services.LoggerService

global DataService dataSvc
global AlertingMessageService jmsSvc
global LoggerService logger

declare RawAlert
    @role( event )
end

declare Alert 
    @role(event) 
end
 
/*
    An Alert is created from a RawAlert
*/
rule "Create New Alert When RawAlert Arrives"
    when
        $s : RawAlert( status == AlertStatus.ACTIVE )
    then
        logger.logMessage("InitialAlert received : " + $s );
        Alert a = new Alert( $s );
        a.setUpstreamLink( dataSvc.lookupUpstreamLink( a.getDeviceName() ) );
        a.setDownstreamLink( dataSvc.lookupDownstreamLink( a.getDeviceName() ) );
        logger.logMessage("InitialAlert received.  Creating Alert for Alert " + a.getId());
        retract($s);
        insert(a);
end

rule "Multiple Alerts"
    duration( 1m )
    when
        $a : Alert()
        $l : List( ) from accumulate( $b : Alert( deviceName == $a.deviceName, this after[0,1m] $a ), collectList($b) )
    then
        AlertGroup alertGroup = new AlertGroup( $a.getDeviceName() );
        alertGroup.addAlerts( $l );
        for ( Alert alert : new ArrayList<Alert>((List<Alert>)$l) ){
            retract( alert );
        }
        logger.logMessage("AlertGroup created: " + alertGroup );
        jmsSvc.sendAlertGroup( alertGroup );
end

rule "Single Alert"
    when
        $a : Alert()
        not Alert( this != $a, deviceName == $a.deviceName, this after[0,59s] $a )
    then
        retract( $a );
        logger.logMessage("Single alert processed: " + $a );
end
